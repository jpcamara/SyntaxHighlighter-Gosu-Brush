<?xml version='1.0' encoding='utf-8' ?> 
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd"> 
<html xmlns='http://www.w3.org/1999/xhtml'>
	<head>
		<title>Gosu Brush Example</title>
	</head>
	<body>
		<p>Example code taken from the example on the <a href="http://gosu-lang.org/intro.shtml">Gosu intro page.</a></p>
		<pre class="brush: gs">
			package example

			uses java.util.*

			uses java.io.File

			class Person extends Contact implements IEmailable {

			  var _name : String
			  var _age : Integer as Age 
			  var _relationship : Relationship as readonly RelationshipOfPerson

			  delegate _emailHelper represents IEmailable

			  enum Relationship {
			    FRIEND,
			    FAMILY,
			    BUSINESS_CONTACT
			  }

			  // Map of names to people
			  static var ALL_PEOPLE = new HashMap&lt;String, Person&gt;()

			  /* Constructs a new Person */
			  construct( name : String, age : Integer, relationship : Relationship ) {
			    _name = name
			    _age = age
			    _relationship = relationship
			    _emailHelper = new EmailHelper( this )
			  }

			  property get Name():String{
			    return _name
			  }

			  property set Name(name : String){
			    _name = name
			  }

			  /* Implement IEmailable#getEmailName() */
			  override function getEmailName():String{
			    return Name
			  }

			  function incrementAge() {
			    _age++
			  }

			  @Deprecated
			  function printPersonInfo() {
			    print( "Person { Name : ${Name}, Age : ${Age}, Relationship : ${RelationshipOfPerson} }" )
			  }

			  static function addPerson(p : Person){
			    if(ALL_PEOPLE.containsKey(p?.Name)) {
			      throw new IllegalArgumentException( "There is already someone named '${p.Name}'." )
			    }
			    ALL_PEOPLE[p.Name] = p
			  }

			  static function addAllPeople( contacts : List&lt;Contact&gt; ) {
			    for( contact in contacts ) {
			      if( contact typeis Person and not ALL_PEOPLE.containsKey( contact.Name )) {
			        addPerson( contact )
			      }
			    }
			  }

			  static function getAllPeopleOlderThanNOrderedByName( age : int ) {
			    var allPeople = ALL_PEOPLE.Values

			    return allPeople.where( \ p -&gt; p.Age &gt; age ).orderBy( \ p -&gt; p.Name )
			  }

			  static function loadPersonFromDB( id : Integer ) {
			    using( var conn = DBConnectionManager.getConnection(),
			      var stmt = conn.prepareStatement( "SELECT name, age, relationship FROM PEOPLE WHERE ID=?") ){

			      stmt.setInt( 0, 0 )
			      var result = stmt.executeQuery()
			      if( result.next() ) {
			         addPerson( new Person( result.getString( "name" ),
			                    result.getInt( "age" ),
			                    Relationship.valueOf( result.getString( "relationship" ) ) ) )

			      }
			    }
			  }

			  /* Loads in people from a CSV */
			  static function loadFromFile( file : File ) {
			    file.eachLine( \ line -&gt; {
			      if( line.HasContent ) {
			        addPerson( line.toPerson() )
			      }
			    })
			  }

			  /* Save people to a CSV */
			  static function saveToFile( file : File ) {
			    using( var writer = new FileWriter( file ) ) {
			      print( PersonCSVTemplate.renderToString( ALL_PEOPLE.Values ) )
			      PersonCSVTemplate.render( writer, ALL_PEOPLE.Values )
			    }
			  }
			}			
		</pre>
		<pre class="brush: gs">
			package example

			enhancement PersonStringEnhancement : String {

			  function toPerson() : Person {
			     var vals = this.split(",")
			     return new Person( vals[0], vals[1] as int, Relationship.valueOf( vals[2] ) ) )
			  }
			}
		</pre>
		<pre class="brush: gs">
			classpath "."

			uses gw.lang.cli.CommandLineAccess

			uses gw.util.Shell

			var args = CommandLineAccess.getRawArgs()

			if( args.Count == 0 ) {
			  print( "You passed in zero arguments" )
			} else {
			  var cmdLine = args.join( " " )
			  print( "Executing '${cmdLine}'" )
			  Shell.exec( cmdLine )
			}
		</pre>		
	</body>
	<link href="http://alexgorbatchev.com/pub/sh/current/styles/shCore.css" rel="stylesheet" type="text/css" />
	<link href="http://alexgorbatchev.com/pub/sh/current/styles/shThemeDefault.css" rel="stylesheet" type="text/css" />
	<script type="text/javascript" src="http://alexgorbatchev.com/pub/sh/current/scripts/shCore.js"></script>
	<script type="text/javascript" src="https://github.com/jpcamara/SyntaxHighlighter-Gosu-Brush/raw/master/shBrushGosu.js"></script>
	<script type="text/javascript">
		SyntaxHighlighter.all();
	</script>
</html>